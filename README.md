# Sensor ultras√≥nico para medir la distancia en un sistema de aparcamiento  

[![Github][github-shield]][github-url]
[![Kofi][kofi-shield]][kofi-url]
[![LinkedIn][linkedin-shield]][linkedin-url]
[![Khanakat][khanakat-shield]][khanakat-url]

## üìì TABLA DE CONTENIDO

1. Componentes necesarios
2. Arduino UNO
3. C√≥mo funciona el sensor ultras√≥nico Arduino
4. El zumbador o buzzer Arduino
5. Sistema de alerta con LEDs
6. Sensor ultras√≥nico montando al circuito
7. Diagramas de flujo de las funciones del c√≥digo fuente
8. Programando el sensor ultras√≥nico Arduino para medir distancia
9. Resultados en interface de hardware
10. Resultados en interface de Software.
11. Implementaci√≥n de reconocimiento de voz con control de dispositivo m√≥vil  

## ‚úîÔ∏è Componentes necesarios

La siguiente lista te muestra todo el material necesario para la implementaci√≥n.  
* Arduino UNO
* Protoboard
* Cables
* 3 resistencias de 220 Œ©
* 1 LED verde
* 1 LED amarillo
* 1 LED rojo
* 1 sensor ultras√≥nico Arduino (HC-SR04)
* 1 buzzer
* 1 bluetooth HC-06

![Materiales](img/Screenshot_1.png?raw=true)

## Arduino UNO

El Arduino es una plataforma computacional f√≠sica open-source basada en una simple tarjeta de I/O y un entorno de desarrollo que implementa el lenguaje Processing/Wiring. El Arduino Uno R3 puede ser utilizado para desarrollar objetos interactivos o puede ser conectado a software de tu computadora (por ejemplo, Flash, Processing, MaxMSP). El IDE open-source puede ser descargado gratuitamente (actualmente para Mac OS X, Windows y Linux).  

![arduino](img/Screenshot_2.png?raw=true)  

## C√≥mo funciona el sensor ultras√≥nico Arduino

Para medir distancias con Arduino podemos hacerlo de diferentes maneras. Existen el sensor de infrarrojos, que utilizan las propiedades de la luz para calcular la distancia, y el sensor ultras√≥nico Arduino utiliza las propiedades de propagaci√≥n del sonido para medir distancias. M√°s concreto utiliza los ultrasonidos. Este tipo de ondas sonoras se encuentran por encima del espectro audible por los seres humanos.

![espectro](img/Screenshot_3.png?raw=true)

El funcionamiento es muy sencillo. El sensor env√≠a una onda ultras√≥nica a trav√©s del disparador o trigger, rebota contra el objeto y el receptor o echo detecta la onda. Sabiendo cu√°nto ha tardado en viajar dicha onda, podemos saber la distancia.

![sensor](img/Screenshot_4.png?raw=true)

Solo hace falta utilizar la famosa f√≥rmula de la velocidad (V = S/T)  
Donde s es el espacio y t es el tiempo. Si despejamos es espacio que es lo que necesitamos saber, nos quedar√≠a de la siguiente manera: S = V x T  

La velocidad del sonido viaja a 343 metros por segundo, osea 34300.0 milisegundos. El tiempo nos lo devolver√° el propio sensor ultras√≥nico Arduino a trav√©s de la API de Arduino. Con todo esto ya podemos calcular a qu√© distancia se encuentra un objeto.  
La velocidad del sonido es 343 m/s a una temperatura de 20¬∫C. La velocidad aumenta o disminuye 0,6 m/s por grado cent√≠grado. Podemos ser m√°s exactos si utilizamos un sensor de temperatura como el LM35.  

## Buzzer Arduino

Para simular correctamente el sensor de distancia vamos a utilizar un buzzer Arduino. Estos componentes utilizan la piezoelectricidad, un fen√≥meno f√≠sico que afecta en determinados cristales (el cuarzo es el m√°s com√∫n). Al someter un cristal de este tipo, se deforman y vibran. Si conseguimos que esa vibraci√≥n tenga una frecuencia dentro del espectro audible, conseguiremos un sonido.  

![buzzer](img/Screenshot_5.png?raw=true)  
Por lo tanto, es importante conocer las frecuencias del espectro audible. Debemos quedarnos con el rango de frecuencias que va de 20 Hz (hercios) a 20 kHz (kilohercio).  
![espectro audible](img/Screenshot_6.png?raw=true)  
Cerca de los 20 Hz el sonido es muy grave. Cuando vamos subiendo de frecuencia, el sonido se va haciendo cada vez m√°s agudo. Esto nos servir√° para alertar de que nos estamos acercando a un obst√°culo al aparcar.  

## Sistema de alerta con LEDs

Por √∫ltimo, incorporamos el sistema de alerta visual para el sensor ultras√≥nico Arduino. Esto nos permite visualizar si estamos cerca o lejos de un obst√°culo. Con 3 LEDs (verde, amarillo y rojo) conseguimos determinar si estamos lejos, cerca o en zona de peligro.  

## Sensor ultras√≥nico, montando al circuito

Se trata de un circuito muy sencillo. Por un lado vamos a tener toda la parte de alertas, ac√∫stica y visual, y por otra parte el sensor de ultrasonidos. En el siguiente esquema muestra el conexionado.  
![modelo](img/Screenshot_7.png?raw=true)  
Cosas a tener en cuenta. Las resistencias son de 220 Œ© y se colocan en serie con los LEDs. El sensor ultras√≥nico Arduino se conecta a dos pines digitales, uno para el trigger o disparador y otro para el echo o receptor. El buzzer Arduino se conecta a una salida PWM.  

## Diagramas de flujo de las funciones del c√≥digo fuente

![diagrama](img/Screenshot_8.png?raw=true)  
![diagrama](img/Screenshot_9.png?raw=true)  

## Programando el sensor ultras√≥nico Arduino para medir distancia

El sistema de aparcamiento consiste en detectar un objeto a trav√©s del sensor ultras√≥nico y avisar con se√±ales de luz y sonido. Por lo tanto, ya tenemos la primera divisi√≥n, detectar el obst√°culo, alertas con sonido y luces.  
Lo primero que har√© ser√° plantear el algoritmo del sistema de detecci√≥n de obst√°culos.  
De los dos algoritmos anteriores, deducimos que vamos a necesitar varios umbrales de decisi√≥n, uno para cada situaci√≥n. Podemos coger una simple regla y determinarlos.  
* Umbral 1: est√° en zona verde desde 30 cm a 20 cm.
* Umbral 2: est√° en zona amarilla, desde 20 cm a 10 cm.
* Umbral 3: est√° en zona roja, menos de 10 cm.
 Es importante conocer el rango de funcionamiento del sensor de ultrasonidos. Va de 2 cm a 400 cm seg√∫n la hoja de especificaciones t√©cnicas.

## Variables y constantes

A trav√©s del sensor de ultrasonidos vamos a detectar el obst√°culo. Comenzamos a programar declarando las variables y constantes. Siempre que trabajemos con pines digitales o anal√≥gicos, es una buena pr√°ctica declarar una constante por cada uno de ellos.

```
// Pines utilizados
#define LEDVERDE 10
#define LEDAMARILLO 9
#define LEDROJO 8
#define TRIGGER 4
#define ECHO 3
#define BUZZER 2

// Constantes
const float sonido = 34300.0; // Velocidad del sonido en cm/s
const float umbral1 = 30.0;
const float umbral2 = 20.0;
const float umbral3 = 10.0;
```

Definimos los pines para los LEDs, para el sensor de ultrasonidos y para el buzzer Arduino. Luego declaramos 4 constantes. La primera es la velocidad del sonido convirtiendo de metros por segundo a cent√≠metros por segundo. Esto lo hacemos multiplicando por 100. Las siguientes constantes son los umbrales de decisi√≥n que hemos marcado antes.  

## Funci√≥n setup

En la funci√≥n setup iniciamos el monitor serie y ponemos los pines en el modo correspondiente. Los LEDs, el Trigger del sensor de ultrasonidos y el buzzer son en modo salida (OUTPUT). El pin Echo del sensor ultras√≥nico en modo entrada (INPUT).  
Por √∫ltimo vamos a empezar desde un estado conocido, todos los LEDs apagados.  
```
void setup() {
  // Iniciamos el monitor serie
  Serial.begin(9600);

  // Modo entrada/salida de los pines
  pinMode(LEDVERDE, OUTPUT);
  pinMode(LEDAMARILLO, OUTPUT);
  pinMode(LEDROJO, OUTPUT);
  pinMode(ECHO, INPUT);
  pinMode(TRIGGER, OUTPUT);
  pinMode(BUZZER, OUTPUT);

  // Apagamos todos los LEDs
  apagarLEDs();
}
```
```
// Apaga todos los LEDs
void apagarLEDs()
{
  digitalWrite(LEDVERDE, LOW);
  digitalWrite(LEDAMARILLO, LOW);
  digitalWrite(LEDROJO, LOW);
}
```

Las funciones son muy √∫tiles por dos razones, nos permiten ordenar el c√≥digo para que sea m√°s legible y tambi√©n reutilizar el c√≥digo. En este caso vamos a necesitar apagar todos los LEDs m√°s de una vez y es m√°s f√°cil escribir una llamada a una funci√≥n con una l√≠nea de c√≥digo que hacer las 3 llamadas a la funci√≥n digitalWrite para apagar cada LED.  

## Funci√≥n Loop

La funci√≥n loop() contiene el c√≥digo que se repetir√° una y otra vez. Aqu√≠ es donde vamos a ir poniendo todo nuestro algoritmo, el que hemos detallado anteriormente. He dividido esta funci√≥n en varias funciones para hacer el c√≥digo m√°s legible.

```
void loop() {
  // Preparamos el sensor de ultrasonidos
  iniciarTrigger();

  // Obtenemos la distancia
  float distancia = calcularDistancia();

  // Apagamos todos los LEDs
  apagarLEDs();

  // Lanzamos alerta si estamos dentro del rango de peligro
  if (distancia < umbral1)
  {
    // Lanzamos alertas
    alertas(distancia);
  }
}
```

## Funci√≥n para Iniciar sensor ultras√≥nico

Lo primero que hacemos es preparar el sensor de ultrasonidos. Esto lo hacemos con la funci√≥n iniciarTrigger() que manda un pulso. Comienza en estado bajo durante 2 milisegundos, luego 10 milisegundos estado alto y por √∫ltimo ponemos en estado bajo. Esto indica que a continuaci√≥n se mandar√° la se√±al para que la capte el echo.

```
// M√©todo que inicia la secuencia del Trigger para comenzar a medir
void iniciarTrigger()
{
  // Ponemos el Triiger en estado bajo y esperamos 2 ms
  digitalWrite(TRIGGER, LOW);
  delayMicroseconds(2);

  // Ponemos el pin Trigger a estado alto y esperamos 10 ms
  digitalWrite(TRIGGER, HIGH);
  delayMicroseconds(10);

  // Comenzamos poniendo el pin Trigger en estado bajo
  digitalWrite(TRIGGER, LOW);
}
```

## Funci√≥n para calcular la distancia de los objetos

Una vez que est√° preparado el sensor, ya podemos utilizarlo para calcular la distancia. Eso lo hacemos con la funci√≥n calcularDistancia(). Es una funci√≥n particular ya que nos va a devolver un valor. Eso se hace poniendo al final (o donde quieras) la palabra reservada return seguido del valor que quieres devolver. Todo el c√≥digo que haya por debajo del return no se ejecuta as√≠ que precauci√≥n. En este caso yo devuelvo la distancia calculada dentro de la funci√≥n que es una variable del tipo float.  
Para calcular el tiempo se utiliza la funci√≥n nativa de Arduino pulseIn. Esta funci√≥n nos devuelve el tiempo transcurrido hasta que cambia de estado. Anteriormente hemos preparado el sensor dejando el pin, donde tenemos conectado el trigger, en estado bajo. Cuando la onda ultras√≥nica viaja por el aire, rebota en el objeto y es detectada por el echo, el pin cambia de estado a alto (HIGH).  
La funci√≥n pulseIn detecta eso precisamente, y devuelve el tiempo transcurrido en microsegundos. Por este motivo se debe convertir a segundos multiplicando por 0,000001 que es lo mismo que dividir por 1.000.000. Con esta informaci√≥n ya podemos aplicar la f√≥rmula para calcular la distancia en funci√≥n del tiempo y la velocidad.

```
// M√©todo que calcula la distancia a la que se encuentra un objeto.
// Devuelve una variable tipo float que contiene la distancia
float calcularDistancia()
{
  // La funci√≥n pulseIn obtiene el tiempo que tarda en cambiar entre estados, en este caso a HIGH
  unsigned long tiempo = pulseIn(ECHO, HIGH);

  // Obtenemos la distancia en cm, hay que convertir el tiempo en segundos ya que est√° en microsegundos
  // por eso se multiplica por 0.000001
  float distancia = tiempo * 0.000001 * sonido / 2.0;
  Serial.print(distancia);
  Serial.print("cm");
  Serial.println();
  delay(500);

  return distancia;
}
```

El tiempo obtenido por la funci√≥n pulseIn debe ser dividido por 2. Esto es debido a que recorre dos veces la distancia, va hacia el objeto, rebota y vuelve hasta el sensor y por lo tanto tarda el doble.  

## Funci√≥n para Lanzar alertas

Una vez tenemos la distancia calculada ya podemos decidir si estamos en la situaci√≥n de enviar alerta o no. Siempre que la distancia est√© por debajo del primer umbral (umbral del LED verde y el menos restrictivo), lanzaremos la correspondiente alerta visual y sonora.  
Antes de hacer nada es conveniente apagar todos los LEDs. Esto lo hacemos llamando a la funci√≥n que hemos generado anteriormente.  
Para reproducir un sonido con buzzer, utilizamos la funci√≥n nativa de Arduino tone(pin, frecuencia, duraci√≥n). Esta funci√≥n admite hasta 3 par√°metros aunque con los 2 primeros, ser√≠a suficiente.  
* Pin: es el pin donde hemos conectado el buzzer. Recuerda que debe ser un pin PWM.
* Frecuencia: podemos poner un valor entre 20 Hz y 20.000 Hz. Juega con estos valores hasta conseguir un sonido adecuado.
* Duraci√≥n: la duraci√≥n en milisegundos que quieres que dure el sonido.
Si no utilizas el par√°metro de duraci√≥n, el sonido se reproducir√° continuamente hasta que llames a la funci√≥n noTone(pin).  
La funci√≥n comprueba que tipo de alerta hay que lanzar. Si est√° dentro del rango umbral 1 y umbral 2, enciende el LED verde y reproduce un sonido de 2.000 Hz durante 200 milisegundos. Si est√° dentro del rango umbral 2 y umbral 3, enciende el LED amarillo y reproduce un sonido de 2.500 Hz durante 200 milisegundos y si est√° por debajo de umbral 3, enciende el LED rojo y reproduce un sonido de 3.000 Hz durante 200 milisegundos.

```
// Funci√≥n que comprueba si hay que lanzar alguna alerta visual o sonora
void alertas(float distancia)
{
  if (distancia < umbral1 && distancia >= umbral2)
  {
    // Encendemos el LED verde
    digitalWrite(LEDVERDE, HIGH);
    tone(BUZZER, 2000, 200);
  }
  else if (distancia < umbral2 && distancia > umbral3)
  {
    // Encendemos el LED amarillo
    digitalWrite(LEDAMARILLO, HIGH);
    tone(BUZZER, 2500, 200);
  }
  else if (distancia <= umbral3)
  {
    // Encendemos el LED rojo
    digitalWrite(LEDROJO, HIGH);
    tone(BUZZER, 3000, 200);
  }
}
```
## Resultados en interface de hardware

Compilar y grabar el c√≥digo fuente al arduino uno. Luego como vemos en la imagen el sensor detectar√° si alg√∫n objeto se pone delante y medir√° su distancia, y seg√∫n nuestra configuraci√≥n de umbrales obtendremos diferentes resultados de sonido y luces seg√∫n la distancia del objeto.  
![prueba](img/Screenshot_10.png?raw=true)  
Tambi√©n podemos monitorear los valores de la distancias medidas con el Monitor Serie. Como podemos ver en la siguiente imagen.  
![consola](img/Screenshot_11.png?raw=true)  

## Resultados en interface de software

En el IDE de Arduino seleccionamos Programa > Exportar Binarios compilados. Para obtener en nuestro folder donde se grab√≥ el proyecto un archivo con el mismo nombre con una extensi√≥n ‚Äò.HEX‚Äô la cual utilizaremos para configurar nuestro Arduino en el emulador de Proteus.  
![exportar](img/Screenshot_12.png?raw=true)  
Ahora en Proteus cargo las librer√≠as de Arduino para posteriormente poder crear el modelo del diagrama hecho en Fritzing.  
![libreria en proteus](img/Screenshot_13.png?raw=true) 
![libreria en proteus](img/Screenshot_14.png?raw=true) 
Se abrir√° una ventana el cual tiene la ruta de librer√≠as del Proteus, entonces pego mi Arduino.lib y luego cierro la ventana. Cierro Proteus y vuelvo a inicializar el programa.  
![libreria en proteus](img/Screenshot_15.png?raw=true)
Ahora reviso los componentes de Device y podre encontrar los componentes de Arduino.  
![libreria en proteus](img/Screenshot_16.png?raw=true)
![modelo en proteus](img/Screenshot_17.png?raw=true)  
Le damos doble click al Arduino, y le cargamos nuestro archivo grabado con extensi√≥n hexadecimal.  
![modelo en proteus](img/Screenshot_18.png?raw=true)  
![modelo en proteus](img/Screenshot_19.png?raw=true) 
![modelo en proteus](img/Screenshot_20.png?raw=true)  
Y finalmente corremos el programa para emular el resultado que obtuvimos en la muestra de Hardware.  
![modelo en proteus](img/Screenshot_21.png?raw=true) 

## Implementaci√≥n de Reconocimiento de Voz con control de dispositivo m√≥vil

Voy a utilizar una aplicaci√≥n de la pagina http://ai2.appinventor.mit.edu , para dise√±ar la aplicaci√≥n que usar√° mi dispositivo m√≥vil para ejecutar acciones en mi proyecto de Arduino.  
![creando la apk](img/Screenshot_22.png?raw=true)  
En primera instancia creamos nuestra interfaz, en donde insert√≥ 4 componentes.  
* ListPicker
* Button
* SpeechRecognizer
* BluetoothClient
Luego procedemos a crear el algoritmo en Bloques.  
![creando la apk](img/Screenshot_23.png?raw=true)  
El c√≥digo en bloque nos permitir√° la conectividad del reconocimiento de voz a trav√©s del bluetooth, y lo grabar√° en un valor para poder ser tomado como un case del switch del c√≥digo fuente.  
Luego le doy click a Build y se compila el programa.  
![creando la apk](img/Screenshot_24.png?raw=true)  
Ahora a√±adire las siguientes lineas de codigo a mi actual c√≥digo fuente, para poder realizar las pruebas de funcionamiento de los LED utilizando el bluetooth del dispositivo m√≥vil. El cual responder√° a las √≥rdenes que yo de con mi voz a trav√©s del micr√≥fono del dispositivo.  

```
//declaro el dato que se va a capturar
char dato;
```

```
void loop() {
  // Codigo anterior...
  //agregamos un switch con las tres opciones de probar led
  dato = Serial.read();
  switch(dato){
    case'A':
    digitalWrite(LEDVERDE, HIGH);
    delay(3000);
    digitalWrite(LEDVERDE, LOW);
    break;
    case'B':
    digitalWrite(LEDAMARILLO, HIGH);
    delay(3000);
    digitalWrite(LEDAMARILLO, LOW);
    break;
    case'C':
    digitalWrite(LEDROJO, HIGH);
    delay(3000);
    digitalWrite(LEDROJO, LOW);
    break;
  }
}
```
Finalmente compilamos y grabamos el c√≥digo en nuestro arduino para hacer la prueba final del funcionamiento de los Leds utilizando mi voz. El cual responder√° ante las palabras: Verde, Amarillo o Rojo. Las cuales se encender√° por 5 segundos y luego se apagaran, y con eso se comprobar√° que los Leds est√°n en correcto funcionamiento. De otra manera tambi√©n podemos utilizar nuestro sensor el cual nos devolver√° los encendidos seg√∫n los umbrales de distancia acompa√±ado de tres distintas frecuencias de tonos.  

## üì• DEMO

![resultado final](img/Screenshot_25.png?raw=true)
![resultado final](img/Screenshot_26.png?raw=true)

## üìÑ LICENCIA

Este proyecto est√° bajo la Licencia (Licencia MIT) - mire el archivo [LICENSE](LICENSE) para m√°s detalles.

## ‚≠êÔ∏è DAME UNA ESTRELLA

Si esta Implementaci√≥n le result√≥ √∫til o la utiliz√≥ en sus Proyectos, d√©le una estrella. ¬°Gracias! O, si te sientes realmente generoso, [¬°Apoye el proyecto con una peque√±a contribuci√≥n!](https://ko-fi.com/fernandocalmet).

<!--- reference style links --->
[github-shield]: https://img.shields.io/badge/-@fernandocalmet-%23181717?style=flat-square&logo=github
[github-url]: https://github.com/fernandocalmet
[kofi-shield]: https://img.shields.io/badge/-@fernandocalmet-%231DA1F2?style=flat-square&logo=kofi&logoColor=ff5f5f
[kofi-url]: https://ko-fi.com/fernandocalmet
[linkedin-shield]: https://img.shields.io/badge/-fernandocalmet-blue?style=flat-square&logo=Linkedin&logoColor=white&link=https://www.linkedin.com/in/fernandocalmet
[linkedin-url]: https://www.linkedin.com/in/fernandocalmet
[khanakat-shield]: https://img.shields.io/badge/khanakat.com-brightgreen?style=flat-square
[khanakat-url]: https://khanakat.com
